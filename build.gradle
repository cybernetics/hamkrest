
plugins {
    id "org.jetbrains.kotlin.multiplatform" version "1.3.20"
    id "maven-publish"
}

repositories {
    mavenCentral()
}

group = "com.natpryce"
version(hasProperty("-version") ? property("-version") : "SNAPSHOT")

println "building version ${version}"


kotlin {
    jvm {
        compilations.all {
            kotlinOptions {
                jvmTarget = '1.8'
            }
        }
    }
    js()
    macosX64()

    sourceSets {
        commonMain {
            dependencies {
                api kotlin('stdlib-common')
            }
        }

        commonTest {
            dependencies {
                api kotlin('test-common')
                api kotlin('test-annotations-common')
            }
        }

        jvmMain {
            dependencies {
                implementation kotlin('stdlib-jdk8')
                implementation kotlin('reflect')
            }
        }

        jvmTest {
            dependencies {
                api kotlin('test-junit')
                api kotlin('reflect')
            }
        }

        jsMain {
            dependencies {
                api kotlin('stdlib-js')
            }
        }

        jsTest {
            dependencies {
                api kotlin('test-js')
            }
        }

        macosX64Main {
            dependencies {
                api kotlin("stdlib")
            }
        }

        macosX64Test {
            dependencies {
                api kotlin("stdlib")
            }
        }

        all {
            languageSettings {
                languageVersion = '1.3'
                apiVersion = '1.3'
            }
        }
    }
}

tasks.withType(Test) {
    beforeTest { desc ->
        print "${desc.className.substring("com.natpryce.hamkrest.".length())}: ${desc.name.replace("_", " ")}"
    }
    afterTest { desc, result ->
        println " -> ${result.resultType}"
    }
}


wrapper {
    gradleVersion = '5.1.1'
}

